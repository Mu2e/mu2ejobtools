#!/usr/bin/perl -w
#
# A.Gaponenko, 2024
#

use strict;
use warnings;
use Getopt::Long;
use File::Basename;
use JSON;

use lib dirname($0) . '/../perllib/';
use mu2ejobtools;

use Data::Dumper; # for debugging

#================================================================
sub usage() {
    my $self = basename($0);
    return <<EOF
Usage:
        $self [-h|--help] \\
              --clusterpars gcp.json \\
              --process uint

This script is meant to be used by a grid worker process.  It maps the
grid process number in a cluster (e.g. the condor PROCESS environment
variable) into the index of the job that should be run by this
process.  The json file is normally generated by the grid submission
script and contains parameters for the current cluster.
The index is printed on stdout.

There is also the

     --help (or -h) options to print out this text and stop.
EOF
}

#================================================================
my %opt;

GetOptions(\%opt,
           'clusterpars=s',
           'process=i',
           'help',
    )
    or die "\nError processing command line options.\n";

if($opt{'help'}) {
    print usage();
    exit 0;
}

my $jfn = $opt{'clusterpars'};
die "Error: --clusterpars is requred\n" unless defined $jfn;
die "Error: $jfn is not a readable file\n" unless -f $jfn && -r $jfn;

my $proc = $opt{'process'};
die "Error: --process is requred\n" unless defined $proc;
die "--process must be non-negative\n" if $proc < 0;

my $jsstr = do{local(@ARGV,$/)=$jfn; <> };
my $jstop = from_json($jsstr);

my $jobmap = $jstop->{ json_key_jobset() };
die "Error: file $jfn does not contain required jobs data\n"
    unless defined $jobmap;

die "Error: jobs is not an array in file $jfn\n"
    unless ref($jobmap) eq ref([]);

die "Error: --process $proc is out of range for $jfn\n"
    unless $proc < scalar(@$jobmap);

#print "ref jobmap = ",ref($jobmap),"\n";
print $jobmap->[$proc], "\n";

#================================================================

#================================================================
exit(0);
